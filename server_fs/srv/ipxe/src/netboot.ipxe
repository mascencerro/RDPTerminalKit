#!ipxe

#Init networking
dhcp

#Networking info we got from the DHCP server
#echo next-server is ${next-server}
#echo filename is ${filename}
#echo MAC address is ${net0/mac}
#echo IP address is ${ip}

#Set flavor to lts
set flavor lts
#echo flavor is ${flavor}

#Set command line
set cmdline modules=loop,squashfs quiet
#echo cmdline is ${cmdline}

#Server address
set server http://${next-server}/alpine
#echo server is ${server}

#Kernel file
set vmlinuz ${server}/boot/vmlinuz-${flavor}
#echo vmlinuz is ${vmlinuz}
set initramfs ${server}/boot/initramfs-${flavor}
#echo initramfs is ${initramfs}

#Modloop file
set modloop ${server}/boot/modloop-${flavor}
#echo modloop is ${modloop}

#Repository for apk
#Update this if you'd like a newer version of Alpine
#Alternatively, set branch to edge for the absolutel latest
#set mirror http://dl-cdn.alpinelinux.org/alpine
set mirror ${server}
set branch v3.21
set repo ${mirror}/${branch}/main
#echo repo is ${repo}

#apkovl file - set this if you want to apply
#an apkovl file to configure the Alpne instance
set apkovl ${server}/client/thinclient.apkovl.tar.gz
#echo apkovl is ${apkovl}

#Uncomment this if you want to see the information before continuing
#prompt Press any key to continue

#Kernel, initrd
#For EFI, we need to tell the kernel the initrd filename. For BIOS it doens't hurt to leave the initrd argument.
#If you want to use Alpine bare, use this line:
#kernel ${vmlinuz} ${cmdline} alpine_repo=${repo} modloop=${modloop} initrd=initramfs-${flavor}
#If you want to use Alpine with an apkovl, use this line:
kernel ${vmlinuz} ${cmdline} modloop=${modloop} apkovl=${apkovl} initrd=initramfs-${flavor}
initrd ${initramfs}

#Boot
boot

#Pause if errors
prompt Some error occurred, press any key to continue